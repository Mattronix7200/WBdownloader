; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Menad¿er pobierania"
#define MyAppVersion "0.1.83"
#define MyAppPublisher "WindowsBASE.pl"
#define MyAppURL "https://windowsbase.pl/"
#define MyAppExeName "wbdownloader.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
SignTool=sign $f
AppId={{DC3E86C4-D351-43A0-93DE-E09DD36FE43E}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={localappdata}\WBdownloader
DisableDirPage=yes
DisableWelcomePage=no
DefaultGroupName=Menad¿er pobierania- WindowsBASE.pl
AllowNoIcons=yes
; Remove the following line to run in administrative install mode (install for all users.)
; PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=commandline
OutputDir=C:\Users\Jakub\Desktop
OutputBaseFilename=wbd_setup
SetupIconFile=C:\Users\Jakub\Desktop\install1.ico
Compression=lzma2/ultra64
SolidCompression=yes
WizardStyle=modern
WizardSmallImageFile=C:\Users\Jakub\Desktop\logo.bmp
WizardImageFile=C:\Users\Jakub\Desktop\slogo.bmp

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\Users\Jakub\Desktop\WBdownloader\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Jakub\Desktop\WBdownloader\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; These files will be downloaded
Source: "{tmp}\wbsft_data.7z"; DestDir: "{app}\bin"; Flags: external

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
Filename: "{app}\bin\cert\certcUP.exe"; StatusMsg: "Weryfikacja danych..."; Parameters: "/c"; Flags: runhidden shellexec waituntilterminated 
Filename: "{app}\bin\sd\runOnce.exe"; StatusMsg: "Instalacja wymaganych modu³ów..."; Parameters: "/c"; Flags: runhidden shellexec waituntilterminated 

[Messages]
ConfirmUninstall=Czy na pewno chcesz odinstalowaæ aplikacjê "%1"? Pamiêtaj, ¿e podczas usuwania aplikacji, wszystkie aplikacje zostan¹ zamkniête, a niezapisane dane zostan¹ utracone.

[UninstallRun]
Filename: "{cmd}"; Parameters: "/C ""taskkill /im mshta.exe /f /t"   
Filename: "{cmd}"; Parameters: "/C ""taskkill /im fdm.exe /f /t"
Filename: "{cmd}"; Parameters: "/C ""taskkill /im mshta.exe /f /t"
Filename: "{cmd}"; Parameters: "/C ""taskkill /im wget.exe /f /t"
Filename: "{cmd}"; Parameters: "/C ""taskkill /im aria2c.exe /f /t"
Filename: "{cmd}"; Parameters: "/C ""taskkill /im JDownloader2.exe /f /t"

[UninstallDelete]
Type: files; Name: "{app}\*.*"
Type: files; Name: "{app}\bin\*.*"
Type: filesandordirs; Name: "{app}\*.*"
Type: filesandordirs; Name: "{app}\bin\*.*"
Type: dirifempty; Name: "{app}\bin\"
Type: dirifempty; Name: "{app}\bin"
Type: dirifempty; Name: "{app}\"
Type: dirifempty; Name: "{app}"

[Code]
var
  DownloadPage: TDownloadWizardPage;

function OnDownloadProgress(const Url, FileName: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if Progress = ProgressMax then
    Log(Format('Plik pobrany pomyœlnie do: {tmp}: %s', [FileName]));
  Result := True;
end;

procedure InitializeWizard;
begin
  DownloadPage := CreateDownloadPage(SetupMessage(msgWizardPreparing), SetupMessage(msgPreparingDesc), @OnDownloadProgress);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
  if CurPageID = wpReady then begin
    DownloadPage.Clear;
    DownloadPage.Add('https://windowsbase.pl/uploads/app/updates/modules/wbsft_data.7z', 'wbsft_data.7z', '');
    DownloadPage.Show;
    try
      try
        DownloadPage.Download; // This downloads the files to {tmp}
        Result := True;
      except
        if DownloadPage.AbortedByUser then
          Log('Aborted by user.')
        else
          SuppressibleMsgBox(AddPeriod(GetExceptionMessage), mbCriticalError, MB_OK, IDOK);
        Result := False;
      end;
    finally
      DownloadPage.Hide;
    end;
  end else
    Result := True;
end;


function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstallString: String;
begin
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;


{ ///////////////////////////////////////////////////////////////////// }
function IsUpgrade(): Boolean;
begin
  Result := (GetUninstallString() <> '');
end;


{ ///////////////////////////////////////////////////////////////////// }
function UnInstallOldVersion(): Integer;
var
  sUnInstallString: String;
  iResultCode: Integer;
begin
{ Return Values: }
{ 1 - uninstall string is empty }
{ 2 - error executing the UnInstallString }
{ 3 - successfully executed the UnInstallString }

  { default return value }
  Result := 0;

  { get the uninstall string of the old app }
  sUnInstallString := GetUninstallString();
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString, '/SILENT /NORESTART /SUPPRESSMSGBOXES','', SW_HIDE, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else
      Result := 2;
  end else
    Result := 1;
end;

{ ///////////////////////////////////////////////////////////////////// }
procedure CurStepChanged(CurStep: TSetupStep);
begin
  if (CurStep=ssInstall) then
  begin
    if (IsUpgrade()) then
    begin
      UnInstallOldVersion();
    end;
  end;
end;